#pragma kernel ConstructSparseVoxelTree // 0

#include "MarchingCubesResources.hlsl"

// Sparse voxel octree settings
const uint MipmapMaxDepth;
const uint3 TextureMM0Dims;

// Per-pass set variables
const uint MipmapPassDepth;
const int3 LastPassNumCells;

RWTexture3D<int> SurfaceCells;

uint pow2(uint exponent)
{
    return 1 << exponent;
}

bool IsCellOutsideGrid(int3 cell)
{
    return (any(cell) < 0
            || cell.x >= LastPassNumCells.x
            || cell.y >= LastPassNumCells.y
            || cell.z >= LastPassNumCells.z);
}

uint3 GetMipmapOffset(uint mipmapLayer)
{
    if (mipmapLayer == 0) return 0;
    
    uint3 offset = 0;
    offset.x += TextureMM0Dims.x;
    if (mipmapLayer > 1) offset.y = TextureMM0Dims.y - TextureMM0Dims.y / pow2(mipmapLayer-1);

    return offset;
}

int ReadFromMipmapLayer(uint3 pixelID, uint mipmapLayer)
{
    if (IsCellOutsideGrid(pixelID)) return 0;

    uint3 offsetPixelID = GetMipmapOffset(mipmapLayer) + pixelID;

    return SurfaceCells[offsetPixelID];
}

void WriteToMipmapLayer(uint3 pixelID, uint mipmapLayer, int val)
{
    uint3 offsetPixelID = GetMipmapOffset(mipmapLayer) + pixelID;

    SurfaceCells[offsetPixelID] = val;
}

void WriteBit(inout uint val, int bitNr)
{
    val |= (1 << bitNr);
}

[numthreads(TN_MC,TN_MC,TN_MC)]
void ConstructSparseVoxelTree (uint3 id : SV_DispatchThreadID)
{
    uint3 basePixelID = 2 * id.xyz;

    // Check all inner cells for leaves, and write the reseults using bit-wise or-operations
    uint node8bitData = 0;
    for (int i = 0; i < 8; i++)
    {
        uint3 childPixelID = basePixelID + Offsets_2x2x2[i];
        bool containsLeaf = ReadFromMipmapLayer(childPixelID, MipmapPassDepth-1) != 0;

        if (containsLeaf) WriteBit(node8bitData, i);
    }

    WriteToMipmapLayer(id.xyz, MipmapPassDepth, node8bitData);
}