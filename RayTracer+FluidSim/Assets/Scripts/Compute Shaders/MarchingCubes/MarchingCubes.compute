#pragma kernel CalcGridDensities // 0
#pragma kernel FindSurface // 1
#pragma kernel GenerateFluidMesh // 2
#pragma kernel TransferToSpatialLookup // 3
#pragma kernel ConstructSpatialLookupTexture // 4
#pragma kernel TransferToRenderer // 5

#include "MarchingCubesResources.hlsl"

// Marching cubes settings
const float CellSize;
const float DensityRadius;
const float Threshold;
const uint4 NumCells;
const uint NumCellsAll;

// Mesh generation settings
const float DistanceMultiplier;
const float DensityMultiplier;

// Num variables
const uint StaticVerticesNum;
const uint StaticTrisNum;
const uint FluidVerticesNum;
const uint FluidTrisNum;
const uint LastFluidVerticesNum;
const uint LastFluidTrisNum;

RWStructuredBuffer<int2> SpatialLookup; // [NumObjects](sphereIndex, CellKey)
RWStructuredBuffer<int> StartIndices;
RWStructuredBuffer<float3> Points;

AppendStructuredBuffer<MCTri> FluidTriMeshAPPEND;
ConsumeStructuredBuffer<MCTri> FluidTriMeshCONSUME;

RWStructuredBuffer<MCTri> FluidTriMeshSL;
RWStructuredBuffer<int> FluidStartIndices;

RWStructuredBuffer<Triangle> Triangles;
RWStructuredBuffer<Vertex2> Vertices;

RWTexture3D<float> GridDensities;
RWTexture3D<float> SurfaceCells;
RWTexture3D<int2> SurfaceCellsLookup;

bool ValidCell(int3 cell)
{
    return cell.x >= 0 && cell.x < (int)NumCells.x &&
           cell.y >= 0 && cell.y < (int)NumCells.y &&
           cell.z >= 0 && cell.z < (int)NumCells.z;
}

int GetCellKey(int3 cell)
{
    return cell.x + cell.y * NumCells.x + cell.z * NumCells.w;
}

NearInfo GetNearDstCheap(float3 pos)
{
    int3 cell = int3(pos / CellSize);
    int cellKey = GetCellKey(cell);
    int startIndex = StartIndices[cellKey];
 
    NearInfo nearInfo;
    nearInfo.materialIndex = -1;
    float nearDstSqr = FLT_MAX;
    int index = startIndex;
    while (cellKey == SpatialLookup[index].y)
    {
        int pointIndex = SpatialLookup[index].x;
        float3 point2 = Points[pointIndex];
 
        float3 dst = pos - point2;
        float dstSqr = dot2(dst);
        nearDstSqr = min(nearDstSqr, dstSqr);
 
        // --- Increment index each iteration - Cell particle search algorithm ---
 
        index += 1;
    }
 
    nearInfo.val = max(1 - sqrt(nearDstSqr) * DistanceMultiplier, 0);
 
    return nearInfo;
}
 
NearInfo GetNearDst(float3 pos, int radius)
{
    int3 cell = int3(pos / CellSize);
 
    float radiusSqr = sqr(radius);
    NearInfo nearInfo;
    nearInfo.materialIndex = -1;
    float nearDstSqr = FLT_MAX;
    for (int x = -radius; x <= radius; x++)
    {
        for (int y = -radius; y <= radius; y++)
        {
            for (int z = -radius; z <= radius; z++)
            {
                int3 curCell = cell + int3(x, y, z);
 
                if (!ValidCell(curCell)) continue;
 
                int cellKey = GetCellKey(curCell);
                int startIndex = StartIndices[cellKey];
 
                int index = startIndex;
                while (cellKey == SpatialLookup[index].y)
                {
                    int pointIndex = SpatialLookup[index].x;
                    float3 point2 = Points[pointIndex];
           
                    float3 dst = pos - point2;
                    float dstSqr = dot2(dst);

                    nearDstSqr = min(nearDstSqr, dstSqr);
           
                    // --- Increment index each iteration - Cell particle search algorithm ---
           
                    index++;
                }
            }
        }
    }
 
    nearInfo.val = max(1 - sqrt(nearDstSqr) * DistanceMultiplier, 0);
 
    return nearInfo;
}
 
NearInfo GetNearDensity(float3 pos, int radius)
{
    int3 cell = int3(pos / CellSize);

    float radiusSqr = sqr(radius);
    NearInfo nearInfo;
    nearInfo.materialIndex = -1;
    float totDensity = 0.0;
    for (int x = -radius; x <= radius; x++)
    {
        for (int y = -radius; y <= radius; y++)
        {
            for (int z = -radius; z <= radius; z++)
            {
                int3 curCell = cell + int3(x, y, z);
 
                if (!ValidCell(curCell)) continue;
 
                int cellKey = GetCellKey(curCell);
                int startIndex = StartIndices[cellKey];
 
                int index = startIndex;
                while (cellKey == SpatialLookup[index].y)
                {
                    int pointIndex = SpatialLookup[index].x;
                    float3 point2 = Points[pointIndex];
           
                    float3 dst = pos - point2;
                    float dstSqr = dot2(dst);
 
                    if (dstSqr > radiusSqr) { index++; continue; }
 
                    totDensity += MSDensityKernel(sqrt(dstSqr), radius);
           
                    // --- Increment index each iteration - Cell particle search algorithm ---
           
                    index ++;
                }
            }
        }
    }
 
    nearInfo.val = max(totDensity * DensityMultiplier, 0);
 
    return nearInfo;
}

// Similar to a density map, but cheaper
[numthreads(TN_MC,TN_MC,TN_MC)]
void CalcGridDensities (uint3 mcCell : SV_DispatchThreadID)
{
    if (!ValidCell(mcCell)) return;

    float3 pos = mcCell * CellSize;
    NearInfo nearInfo = GetNearDstCheap(pos);

    GridDensities[mcCell] = nearInfo.val;
}

[numthreads(TN_MC,TN_MC,TN_MC)]
void FindSurface (uint3 baseCell : SV_DispatchThreadID)
{
    if (!ValidCell(baseCell+1)) return;

    bool c0_0_0 = GridDensities[baseCell] < Threshold;
    bool c1_0_0 = GridDensities[baseCell + uint3(1,0,0)] < Threshold;
    bool c0_1_0 = GridDensities[baseCell + uint3(1,0,1)] < Threshold;
    bool c1_1_0 = GridDensities[baseCell + uint3(0,0,1)] < Threshold;
    bool c0_0_1 = GridDensities[baseCell + uint3(0,1,0)] < Threshold;
    bool c1_0_1 = GridDensities[baseCell + uint3(1,1,0)] < Threshold;
    bool c0_1_1 = GridDensities[baseCell + uint3(1,1,1)] < Threshold;
    bool c1_1_1 = GridDensities[baseCell + uint3(0,1,1)] < Threshold;

    bool isSurfaceCell = (c0_0_0 || c1_0_0 || c0_1_0 || c1_1_0 || c0_0_1 || c1_0_1 || c0_1_1 || c1_1_1)
                          && !(c0_0_0 && c1_0_0 && c0_1_0 && c1_1_0 && c0_0_1 && c1_0_1 && c0_1_1 && c1_1_1);

    SurfaceCells[baseCell] = isSurfaceCell;
}

float3 interpolateVerts(float d1, float d2, float3 v1, float3 v2, float Threshold)
{
    // Division by 0?
    float t = saturate((Threshold - d1) / (d2 - d1));
    
    return lerp(v1, v2, t);
}

[numthreads(TN_MC,TN_MC,TN_MC)]
void GenerateFluidMesh (uint3 baseCell : SV_DispatchThreadID)
{
    if (!ValidCell(baseCell+1) || SurfaceCells[baseCell] != 1) return;

    uint3 localPositions[8] = {
        baseCell,
        baseCell + uint3(1,0,0),
        baseCell + uint3(1,0,1),
        baseCell + uint3(0,0,1),
        baseCell + uint3(0,1,0),
        baseCell + uint3(1,1,0),
        baseCell + uint3(1,1,1),
        baseCell + uint3(0,1,1)
    };

    float densities[8];
    for (int i = 0; i < 8; i++)
    {
        // Will this work for edges?
        uint3 localPos = localPositions[i];
        densities[i] = GridDensities[localPos];
    }

    // Calculate unique index for each cube configuration.
    // There are 256 possible values
    // A value of 0 means the cube is entirely inside the surface; 255 entirely outside.
    // The final cubeIndex value is used to look up corresponding edges from the edge table the edge table, that is how the cube is cut by the isosurface
    int cubeIndex = 0;
    for (int j = 0; j < 8; j++) if (densities[j] > Threshold) cubeIndex |= (uint)pow(2, j);

    // Create triangles from the edges
    int edges[16] = triangulation[cubeIndex];
    for (int k = 0; edges[k] != -1; k +=3)
    {
        // Get indices of corner points A and B for each of the three edges
        // of the cube that need to be joined to form the triangle.
        int a0 = cornerIndexAFromEdge[edges[k]];
        int b0 = cornerIndexBFromEdge[edges[k]];

        int a1 = cornerIndexAFromEdge[edges[k+1]];
        int b1 = cornerIndexBFromEdge[edges[k+1]];

        int a2 = cornerIndexAFromEdge[edges[k+2]];
        int b2 = cornerIndexBFromEdge[edges[k+2]];

        MCTri tri;
        tri.cellKey = GetCellKey(baseCell);
        tri.vertex0 = interpolateVerts(densities[a0], densities[b0], localPositions[a0], localPositions[b0], Threshold);
        tri.vertex1 = interpolateVerts(densities[a1], densities[b1], localPositions[a1], localPositions[b1], Threshold);
        tri.vertex2 = interpolateVerts(densities[a2], densities[b2], localPositions[a2], localPositions[b2], Threshold);

        // Transform to world space
        tri.vertex0 *= CellSize;
        tri.vertex1 *= CellSize;
        tri.vertex2 *= CellSize;

        FluidTriMeshAPPEND.Append(tri);
    }
}

float GetMCTriArea(MCTri tri)
{
    float3 ab = tri.vertex1 - tri.vertex0;
    float3 ac = tri.vertex2 - tri.vertex0;

    float3 crossProduct = cross(ab, ac);
    float area = length(crossProduct) * 0.5;
    return area;
}

float3 GetPlaneNormal (MCTri tri)
{
    float3 ab = tri.vertex1 - tri.vertex0;
    float3 ac = tri.vertex2 - tri.vertex0;

    return normalize(cross(ab, ac));
}

bool IsUnused(MCTri tri)
{
    return tri.cellKey == 0 && all(tri.vertex0 == 0) && all(tri.vertex1 == 0) && all(tri.vertex2 == 0);
}

[numthreads(TN_MC2,1,1)]
void TransferToSpatialLookup (uint3 id : SV_DispatchThreadID)
{
    uint i = id.x;
    if (i >= max(FluidTrisNum, LastFluidTrisNum)) return;

    MCTri mcTri = FluidTriMeshCONSUME.Consume();

    if (IsUnused(mcTri)) mcTri.cellKey = NumCellsAll;

    FluidTriMeshSL[i] = mcTri;
}

[numthreads(TN_MC,TN_MC,TN_MC)]
void ConstructSpatialLookupTexture (uint3 cellID : SV_DispatchThreadID)
{
    if (!ValidCell(cellID) || SurfaceCells[cellID] != 1) return;

    int cellKey = GetCellKey(cellID);
    uint startIndex = FluidStartIndices[cellKey];

    uint index = startIndex;
    while (cellKey == FluidTriMeshSL[index].cellKey && index < FluidTrisNum)
    {
        index += 1;
    }

    uint celltrisNum = index - startIndex;

    SurfaceCellsLookup[cellID] = uint2(StaticTrisNum + startIndex, celltrisNum);
}

[numthreads(TN_MC2,1,1)]
void TransferToRenderer (uint3 id : SV_DispatchThreadID)
{
    uint i = id.x;
    if (i >= max(FluidTrisNum, LastFluidTrisNum)) return;

    MCTri mcTri = FluidTriMeshSL[i];

    uint vertexBaseIndex = 3 * i + StaticVerticesNum;

    Vertices[vertexBaseIndex] = InitVertex(mcTri.vertex0, float2(-1, -1));
    Vertices[vertexBaseIndex + 1] = InitVertex(mcTri.vertex1, float2(-1, -1));
    Vertices[vertexBaseIndex + 2] = InitVertex(mcTri.vertex2, float2(-1, -1));

    float3 localNormal = GetPlaneNormal(mcTri);
    float area = GetMCTriArea(mcTri);

    // Create an "empty" triangle if i is outside of the current frame's mesh length, but inside the last frame's mesh length
    // This requires for all unused triangles this frame to be the last elements of the sorted mesh buffer
    if (i < FluidTrisNum) Triangles[i + StaticTrisNum] = InitTriangle(vertexBaseIndex, vertexBaseIndex + 1, vertexBaseIndex + 2, localNormal, area);
    else Triangles[i + StaticTrisNum] = InitTriangle();
}